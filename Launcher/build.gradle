def getVersion() {
    //Major.Minor.Buildnumber.Buildtype
    def major = 1
    def minor = 0
    def buildNumber = System.getenv('BUILD_NUMBER') //Generated by Jenkins
    def buildType = System.getenv('BUILD_TYPE') //Generated by Jenkins
    def formedVer = major + '.' + minor + '.' + buildNumber + '-' + buildType
    println 'Building Version: ' + formedVer
    return formedVer
}

archivesBaseName = 'TCLauncher'
version = getVersion()

dependencies {
	compile 'org.apache.logging.log4j:log4j-core:2.3'
	compile 'com.google.code.gson:gson:2.3.1'
	compile 'commons-codec:commons-codec:1.10'
	compile 'org.apache.commons:commons-compress:1.9'
	compile 'org.tukaani:xz:1.5'
	compile 'org.jcraft:jorbis:0.0.17'
	compile 'org.lwjgl.lwjgl:lwjgl:2.9.3'
	compile 'org.slick2d:slick2d-core:1.0.1'
}

jar {
	from {
        configurations.compile.collect {
            return it.isDirectory() ? it : zipTree(it).matching { 
            	include '**/*.*'
            	exclude '*.dll'
            	exclude '*.so'
            	exclude '*.dylib'
            	exclude '*.jnilib'
                exclude '*.html'
            } 
        }
    }
	manifest {
    	attributes("Implementation-Version": project.version)
        attributes("Main-Class": "tk.tfsthiago1112.Tecnocraft.Launcher.start.JarStart")
    }
}

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

dependencies {
    deployerJars 'org.apache.maven.wagon:wagon-webdav-jackrabbit:2.9'
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
            }
            pom.project {
                name project.archivesBaseName
                packaging 'jar'
                description 'Tecnocraft Launcher'
                url 'http://tfsthiago1112.tk/projects/tecnocraft/launcher.html'

                licenses {
                    license {
                        name 'Eclipse Public Licence'
                        url ''
                    }
                }

                developers {
                    developer {
                        id 'TFSThiago1112'
                        name 'TFSThiago1112'
                        roles { role 'coredeveloper' }
                    }
                }
            }
        }
    }
}